(defmodule LambdaShorthand
  ; (private max)
  (hidden max)
  (defndynamic max [a b]
   (if (< a b) b a))

  ; (private range-to)
  (hidden range-to)
  (defndynamic range-to [target]
    (unreduce (curry + 1) 0 target (list)))

  ; (private sym-from-num)
  (hidden sym-from-num)
  (defndynamic sym-from-num [num]
    (Symbol.from (Dynamic.str gen "-arg-n" num)))

  ; (private gen-args)
  (hidden gen-args)
  (defndynamic gen-args [gen arg-num]
    (collect-into
      (map LambdaShorthand.sym-from-num (LambdaShorthand.range-to arg-num))
      array))

  ; (private str-is-number?)
  (hidden str-is-number?)
  (defndynamic str-is-number? [s]
    (all? (fn [char] (number? (parse char)))
      (String.to-list s)))

  ; (private placeholder?)
  (hidden placeholder?)
  (defndynamic placeholder? [sym]
    (all? (fn [f] (f sym))
      '(symbol?
        (fn [sy] (= (String.head (Symbol.str sy)) ":"))
        (fn [sy] (not (String.empty? (String.tail (Symbol.str sy)))))
        (fn [sy] (LambdaShorthand.str-is-number? (String.tail (Symbol.str sy)))))))

  ; (private get-number-from-placeholder)
  (hidden get-number-from-placeholder)
  (defndynamic get-number-from-placeholder [sym]
    (parse (String.tail (Symbol.str sym))))

  ; (private find-highest-num)
  (hidden find-highest-num)
  (defndynamic find-highest-num [form]
    (walk (fn [x]
           (cond
             (LambdaShorthand.placeholder? x)
             (LambdaShorthand.get-number-from-placeholder x)
             (or (list? x) (array? x))
             (LambdaShorthand.find-highest-num x)
             0))
          (fn [final-form] (reduce LambdaShorthand.max 0 final-form))
          form))

  ; (private transform)
  (hidden transform)
  (defndynamic transform [gen in]
    (postwalk
     (fn [x]
      (if (LambdaShorthand.placeholder? x)
        (let [num (LambdaShorthand.get-number-from-placeholder x)]
          (LambdaShorthand.sym-from-num num))
        x))
     in))

  (hidden shorthand)
  (defndynamic shorthand [r]
    (let [gen (gensym-with 'lambda-shorthand)
          arg-num (LambdaShorthand.find-highest-num r)
          body (LambdaShorthand.transform gen r)
          args (LambdaShorthand.gen-args gen arg-num)]
      `(fn %args %body))))

(defmacro : [:rest r]
  (LambdaShorthand.shorthand r))

